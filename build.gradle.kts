@Suppress("DSL_SCOPE_VIOLATION")
plugins {
    id("com.android.application") apply false
    id("com.android.library") apply false
    kotlin("android") apply false
    alias(libs.plugins.detekt)
    alias(libs.plugins.ktlint)
    base
}

allprojects {
    group = PUBLISHING_GROUP
}
val ktlintVersion = libs.versions.ktlint.get()
subprojects {
    apply {
        plugin("io.gitlab.arturbosch.detekt")
        plugin("org.jlleitschuh.gradle.ktlint")
    }

    ktlint {
        debug.set(false)
        version.set(ktlintVersion)
        verbose.set(true)
        android.set(false)
        outputToConsole.set(true)
        ignoreFailures.set(false)
        enableExperimentalRules.set(true)
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }

    detekt {
        config = rootProject.files("tools/detekt/detekt.yml")
        reports {
            html {
                required.set(true)
                outputLocation.set(file("build/reports/detekt.html"))
            }
        }
    }
}
